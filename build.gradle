// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0-alpha5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--long'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

ext {
    supportLibVersion = '23.2.1'

    versionCode = getVersionCode()
    versionName = getVersionName()

    pomGroupId = 'tw.idv.palatis'

    scmUrl = 'https://github.com/Palatis/EsptouchForAndroid'
    scmConnectionUrl = 'https://github.com/Palatis/EsptouchForAndroid'
    scmDeveloperConnectionUrl = 'git+ssh://git@github.com:Palatis/EsptouchForAndroid.git'
}

allprojects {
    repositories {
        jcenter()
    }
}